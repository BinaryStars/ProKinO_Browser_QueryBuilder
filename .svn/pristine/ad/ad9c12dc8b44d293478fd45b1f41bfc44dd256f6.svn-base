PREFIX     rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX    rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>
PREFIX     afn: <http://jena.hpl.hp.com/ARQ/function#>

SELECT (STR(?subdomainName) AS ?Subdomain_Name) 
       ?Between_Subdomains_Details
       (COUNT(*) AS ?No_Mutations) WHERE
{
    ?gene          rdf:type                       prokino:Gene .
    ?gene          prokino:hasPrimaryName         "FLT3" .
    ?gene          prokino:presentIn              prokino:Human .
    ?gene          prokino:hasMutation            ?mutation .
    ?mutation      prokino:locatedIn              ?subdomain .
    ?mutation      prokino:hasPrimarySite         "haematopoietic_and_lymphoid_tissue" .  
    ?mutation      rdf:type                       prokino:Missense .
    ?subdomain     rdf:type                       ?subdomainType .
    ?subdomainType rdfs:label                     ?subdomainName
    OPTIONAL { 
         ?subdomain rdf:type    prokino:BetweenSubDomains .
	 ?subdomain rdfs:label  ?betweenType
	 BIND( afn:substring(STR(?betweenType), 22) AS ?Between_Subdomains_Details )
    } 
}
GROUP BY ?subdomainType ?subdomainName ?Between_Subdomains_Details
ORDER BY DESC(?No_Mutations)
