PREFIX     rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX    rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>

SELECT (STR(?subdomainName) AS ?Subdomain_Name) (COUNT(*) AS ?No_Mutations) WHERE
{
    ?gene      rdf:type            prokino:Gene .
    ?gene      prokino:presentIn   prokino:Human .
    ?gene      prokino:hasMutation ?mutation .
    ?mutation  prokino:locatedIn   ?subdomain .
    ?subdomain rdf:type            ?subdomainType .
    ?subdomainType rdfs:label      ?subdomainName
}
GROUP BY ?subdomainType ?subdomainName
ORDER BY DESC(?No_Mutations)
