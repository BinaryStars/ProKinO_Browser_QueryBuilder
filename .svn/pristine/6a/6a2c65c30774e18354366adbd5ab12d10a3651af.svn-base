PREFIX     rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX    rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>


SELECT (str(?cancername) AS ?Cancer_Type) (COUNT(*) AS ?No_Genes) WHERE
{
  ?cancer rdf:type   prokino:Cancer .
  ?cancer rdfs:label ?cancername .
  {
    SELECT ?gene ?cancer WHERE
    {
       ?gene  rdf:type                       prokino:Gene .
       ?gene  prokino:presentIn              prokino:Human .
       ?gene  prokino:hasMutation            ?mut .
       ?mut   prokino:implicatedIn           ?cancer .
    }
    GROUP BY ?gene ?cancer
  }
}
GROUP BY ?cancer ?cancername
HAVING (count(*) >= 2)
ORDER BY desc(?No_Genes)
