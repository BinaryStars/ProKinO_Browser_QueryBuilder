PREFIX     rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX    rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>

SELECT (STR(?geneName) AS ?Gene_Name) (COUNT(*) AS ?No_CancerTypes) WHERE
{
  ?gene rdf:type               prokino:Gene .
  ?gene prokino:hasPrimaryName ?geneName .
  {  
    SELECT ?gene ?cancer WHERE
    {
       ?gene  rdf:type             prokino:Gene .
       ?gene  prokino:presentIn    prokino:Human .
       ?gene  prokino:hasMutation  ?mut .
       ?mut   prokino:implicatedIn ?cancer
    }
    GROUP BY ?gene ?cancer
  }
}
GROUP BY ?geneName
ORDER BY DESC(?No_CancerTypes)
LIMIT 10
