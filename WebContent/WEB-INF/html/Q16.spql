PREFIX     rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX    rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prokino: <http://om.cs.uga.edu/prokino/2.0/#>

SELECT (STR(?geneName) as ?Gene) 
       (STR(?rname) as ?Reaction)
WHERE
{

 SELECT distinct ?rname ?geneName WHERE
 {
   {
     SELECT distinct ?gene WHERE
     {
       ?gene       rdf:type                prokino:Gene .
       ?gene       prokino:presentIn       prokino:Human .
       ?gene       prokino:hasMutation     ?mut .
     }
   }
  
   ?gene           prokino:hasPrimaryName  ?geneName .
   filter(STR(?geneName) = 'EGFR')

   ?container      prokino:includes*       ?gene .
   { { ?reaction   prokino:consumes        ?container }
     UNION
     { ?reaction   prokino:produces        ?container }
     UNION
     { ?reaction   prokino:hasCatalyst     ?container }
   }
   ?reaction       rdf:type                prokino:Reaction .
   ?reaction       prokino:hasPrimaryName  ?rname .
 
 }
}
